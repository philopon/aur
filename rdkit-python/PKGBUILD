_pkgname=RDKit

pkgname="rdkit-python"
pkgver=2017_09_1
pkgrel=3
pkgdesc="RDKit - A collection of cheminformatics and machine-learning software written in C++ and Python."
arch=("i686" "x86_64")
url="http://rdkit.org/"
license=("New BSD License")
boostmajor=1
boostminor=65
pythonmajor=3
pythonminor=6
boostver=$boostmajor.$boostminor
pythonver=$pythonmajor.$pythonminor
boostnext=$boostmajor.$((boostminor + 1))
pythonnext=$pythonmajor.$((pythonminor + 1))

makedepends=("eigen" "bison" "boost>=${boostver}" "boost<${boostnext}" "cmake" "flex" "python>=${pythonver}" "python<${pythonnext}" "python-numpy" "python-six" "python-pillow" "python2-numpy" "python2-six" "python2-pillow" "sqlite3")
depends=("boost-libs>=${boostver}" "boost-libs<${boostnext}" "sqlite" "python>=${pythonver}" "python<${pythonnext}" "python-numpy")

source=(https://github.com/rdkit/rdkit/archive/Release_${pkgver}.tar.gz boost-1.65.patch)
sha256sums=("6a6718896452cc55b84db6f59e95c6a91cc4091b2cda9960b74542363524808b" SKIP)

prepare() {
    cd $srcdir/rdkit-Release_${pkgver}
    patch -p1 < ../boost-1.65.patch
}

build() {
    mkdir -p $srcdir/rdkit-Release_${pkgver}/build
    cd $srcdir/rdkit-Release_${pkgver}/build

    cmake .. \
        -DCMAKE_BUILD_TYPE=Release \
        -DRDK_INSTALL_INTREE=OFF \
        -DRDK_BUILD_INCHI_SUPPORT=ON \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DPYTHON_LIBRARY=/usr/lib/libpython${pythonver}m.so \
        -DPYTHON_INCLUDE_DIR=/usr/include/python${pythonver}m/ \
        -DPYTHON_EXECUTABLE=/usr/bin/python3
    make

}

check() {
    pip3 install pandas --user

    RDBASE=$srcdir/rdkit-Release_${pkgver}
    cd $srcdir/rdkit-Release_${pkgver}/build

    make DESTDIR=fakeinstall install
    RDBASE=$RDBASE PYTHONPATH=`pwd`/fakeinstall/usr/lib/python$pythonver/site-packages LD_LIBRARY_PATH=`pwd`/fakeinstall/usr/lib ctest
}

package() {
    cd $srcdir/rdkit-Release_${pkgver}/build
    make DESTDIR=${pkgdir} install
}
