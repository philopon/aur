pkgname=rDock
pkgver=2013.1
pkgrel=0
pkgdesc='A Fast, Versatile and Open Source Program for Docking'
arch=('x86_64')
url='http://rdock.sourceforge.net'
license=('LGPL3')
depends=('gcc5' 'popt' 'glibc')
makedepends=('cppunit')

source=("http://downloads.sourceforge.net/project/rdock/${pkgname}_${pkgver}_src.tar.gz" 'makefile.patch' 'tmakelib.patch')
sha256sums=('e716998c3f8a2a70205a8d30ba22675bfdb1764d13c858645138c5eadf2a37e9' SKIP SKIP)

_builddir=${pkgname}_${pkgver}_src/build

prepare() {
    cd $_builddir
    patch -p1 < ../../makefile.patch
    patch -p1 < ../../tmakelib.patch
}

build() {
    cd $srcdir/$_builddir
    make linux-g++-64
}

check() {
    cd $srcdir/$_builddir
    make test
}

package() {
    cd $srcdir/$_builddir
    mkdir -p $pkgdir/usr/{lib,bin,share/rDock}
    mkdir -p $pkgdir/etc/profile.d
    echo "export RBT_ROOT=/usr/share/rDock" > $pkgdir/etc/profile.d/rDock.sh
    chmod +x $pkgdir/etc/profile.d/rDock.sh
    cp -rd ../data $pkgdir/usr/share/rDock/data
    cp -d linux-g++-64/release/exe/* $pkgdir/usr/bin
    cp -d linux-g++-64/release/lib/* $pkgdir/usr/lib
}
