# $Id$
# Maintainer: Anatol Pomozov <anatol dot pomozov at gmail>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>

pkgname=openmpi2
pkgver=2.1.0
pkgrel=1
pkgdesc='High performance message passing library (MPI)'
arch=(i686 x86_64)
url='http://www.open-mpi.org'
license=(custom)
depends=(libltdl hwloc)
provides=(openmpi)
conflicts=(openmpi)
makedepends=(inetutils valgrind gcc-fortran)
optdepends=('gcc-fortran: fortran support')
options=(staticlibs)
source=(https://www.open-mpi.org/software/ompi/v${pkgver%.*}/downloads/${pkgname%2}-${pkgver}.tar.gz)
sha1sums=('898687b7567f5925b4a19c608f0f4946066649ae')

build() {
   cd ${pkgname%2}-$pkgver

   ./configure --prefix=/usr \
               --sysconfdir=/etc/${pkgname} \
               --enable-mpi-fortran=all \
               --libdir=/usr/lib/${pkgname} \
               --with-threads=posix \
               --enable-smp-locks \
               --with-valgrind \
               --enable-memchecker \
               --enable-pretty-print-stacktrace \
               --without-slurm \
               --with-hwloc=/usr \
               --with-libltdl=/usr  \
               FC=/usr/bin/gfortran \
               LDFLAGS="$LDFLAGS -Wl,-z,noexecstack"

   make
}

check() {
   cd ${pkgname%2}-$pkgver

   make check
}

package() {
   cd ${pkgname%2}-$pkgver
   make DESTDIR="$pkgdir" install

   # FS#28583
   # install -d -m 755 "$pkgdir"/usr/lib/pkgconfig
   # for i in ompi-c.pc ompi-cxx.pc ompi-f77.pc ompi-f90.pc ompi.pc; do
   #    ln -sf /usr/lib/openmpi/pkgconfig/$i "$pkgdir"/usr/lib/pkgconfig/
   # done

   # install -d -m 755 "$pkgdir"/etc/ld.so.conf.d
   # echo "/usr/lib/$pkgname" > "$pkgdir"/etc/ld.so.conf.d/$pkgname.conf

   # install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

